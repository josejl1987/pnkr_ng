#version 460
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_EXT_samplerless_texture_functions : require

layout(set = 1, binding = 0) uniform texture2D bindlessTextures[];
layout(set = 1, binding = 1) uniform sampler bindlessSamplers[];
layout(set = 1, binding = 4) uniform writeonly image2D bindlessStorageImages[];

layout(local_size_x = 16, local_size_y = 16) in;

layout(push_constant) uniform BlurParams {
    uint inputTexID;
    uint outputTexID;
    uint depthTexID;
    uint samplerID;
    int axis; // 0 = horizontal, 1 = vertical
    float sharpness;
    float zNear;
    float zFar;
} pc;

float linearizeDepth(float depth) {
    return (2.0 * pc.zNear) / (pc.zFar + pc.zNear - depth * (pc.zFar - pc.zNear));
}

// Helper for bindless sampling
vec4 textureBindless2D(uint textureid, uint samplerid, vec2 uv) {
    return texture(sampler2D(bindlessTextures[nonuniformEXT(textureid)], bindlessSamplers[nonuniformEXT(samplerid)]), uv);
}

void main() {
    ivec2 size = imageSize(bindlessStorageImages[nonuniformEXT(pc.outputTexID)]);
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
    if (coord.x >= size.x || coord.y >= size.y) return;

    vec2 uv = (vec2(coord) + 0.5) / vec2(size);
    vec2 texelSize = 1.0 / vec2(size);
    vec2 direction = (pc.axis == 0) ? vec2(texelSize.x, 0.0) : vec2(0.0, texelSize.y);

    float centerDepth = linearizeDepth(textureBindless2D(pc.depthTexID, pc.samplerID, uv).r);
    
    float totalWeight = 0.0;
    float result = 0.0;

    // 9-tap filter
    for(int i = -2; i <= 2; ++i) {
        vec2 offsetUV = uv + direction * float(i);
        
        float sampleDepth = linearizeDepth(textureBindless2D(pc.depthTexID, pc.samplerID, offsetUV).r);
        float weight = 1.0 / (1.0 + abs(centerDepth - sampleDepth) * pc.sharpness); // Bilateral weight
        
        // Gaussian spatial weight (approx)
        weight *= (i==0 ? 1.0 : (abs(i)==1 ? 0.8 : 0.5));

        result += textureBindless2D(pc.inputTexID, pc.samplerID, offsetUV).r * weight;
        totalWeight += weight;
    }

    imageStore(bindlessStorageImages[nonuniformEXT(pc.outputTexID)], coord, vec4(result / totalWeight));
}